#### Main Commands
<ModelCommand>       ::= 'MODEL:'
<SelectCommand>      ::= 'SELECT:'
<GroupCommand>       ::= 'GROUPBY:'
<OrderCommand>       ::= 'ORDERBY:'
<WhereCommand>       ::= 'WHERE:'
#### Aggregation Commands:
<SumAggregator>      ::= 'SUM:'
<AvgAggregator>      ::= 'AVG:'
<CountAggregator>    ::= 'COUNT:'
<MinAggregator>      ::= 'MIN:'
<MaxAggregator>      ::= 'MAX:'
<Aggregator>         ::= <SumAggregator> | <AvgAggregator> | <CountAggregator> | <MinAggregator> | <MaxAggregator>
#### Basic Types
<Integer>            ::= -?[1-9][0-9]*
<String>             ::= (["'])(?:(?=(\\?))\2.)*?\1
<Identifier>         ::= [a-zA-Z][a-zA-Z0-9_]*
#### Comparators
<EqComparator>       ::= '=='
<GeqComparator>      ::= '>='
<LeqComparator>      ::= '<='
<GreaterComparator>  ::= '>'
<LessComparator>     ::= '<'
<NeqComparator>      ::= '!='
<Comparator>         ::= <EqComparator> | <GeqComparator> | <LeqComparator> | <GreaterComparator> | <LessComparator> | <NeqComparator>
#### Operators
<AndOperator>        ::= 'AND'
<OrOperator>         ::= 'OR'
<XorOperator>        ::= 'XOR'
<LogicalOperator>    ::= <AndOperator> | <OrOperator> | <XorOperator>
#### Seperators
<LeftBracket>        ::= '('
<RightBracket>       ::= ')'
<Dot>                ::= '.'
<Comma>              ::= ','
#### Grammar
<ModelName>          ::= <Identifier>
<FieldName>          ::= <Identifier> (<Dot> <Identifier>)*

<Aggregation>        ::= <Aggregator> <FieldName>

<Comparision>        ::= <FieldName> <Comparator> <FieldName> | <FieldName> <Comparator> <Integer> | <FieldName> <Comparator> <String> | <Integer> <Comparator> <FieldName> | <String> <Comparator> <FieldName>
<Expression>         ::= <Comparision> | <LeftBracket> <Expression> <RightBracket> | <Expression> <LogicalOperator> <Expression>

<Model>              ::= <ModelCommand> <ModelName>
<Select>             ::= <SelectCommand> <LeftBracket> (<FieldName> | <Aggregation>) (<Comma> (<FieldName> | <Aggregation>))* <RightBracket>
<GroupBy>            ::= <GroupCommand> <LeftBracket> <FieldName> (<Comma> <FieldName>)* <RightBracket>
<OrderBy>            ::= <OrderCommand> <LeftBracket> <FieldName> (<Comma> <FieldName>)* <RightBracket>
<Where>              ::= <WhereCommand> <Expression>

<Query>              ::= <Model> <Select> <GroupBy>? <OrderBy>? <Where>?
